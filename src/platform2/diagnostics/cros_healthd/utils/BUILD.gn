# Copyright 2020 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

# Library that provides utilities used by cros_healthd.
pkg_config("libcros_healthd_utils_pkg_deps") {
  pkg_deps = [
    "fwupd",
    "libbrillo",
    "libchrome",
    "libmetrics",
    "libmojo",
    "libudev",
    "libusb-1.0",
  ]
}

source_set("libcros_healthd_utils") {
  configs += [ ":libcros_healthd_utils_pkg_deps" ]
  sources = [
    "callback_barrier.cc",
    "fwupd_utils.cc",
    "memory_info.cc",
    "metrics_utils.cc",
    "mojo_utils.cc",
    "procfs_utils.cc",
    "resource_queue.cc",
    "usb_utils.cc",
  ]
  deps = [
    "//diagnostics/base",
    "//diagnostics/mojom/public:cros_healthd_mojo_bindings",
  ]
}

if (use.test || use.fuzzer) {
  pkg_config("libcros_healthd_utils_test_utils_pkg_deps") {
    pkg_deps = [
      "libchrome",
      "libmojo",
    ]
  }

  source_set("libcros_healthd_utils_test_utils") {
    configs += [ ":libcros_healthd_utils_test_utils_pkg_deps" ]
    deps = [ "//diagnostics/mojom/public:cros_healthd_mojo_bindings" ]
    sources = [ "mojo_type_utils.cc" ]
  }
}

if (use.test) {
  pkg_config("libcros_healthd_utils_test_pkg_deps") {
    pkg_deps = [
      "fwupd",
      "libbrillo",
      "libchrome",
      "libchrome-test",
      "libmetrics",
    ]
  }

  source_set("libcros_healthd_utils_test") {
    sources = [
      "callback_barrier_test.cc",
      "dbus_utils_test.cc",
      "fwupd_utils_test.cc",
      "memory_info_test.cc",
      "metrics_utils_test.cc",
      "procfs_utils_test.cc",
      "resource_queue_test.cc",
      "usb_utils_test.cc",
    ]
    configs += [ ":libcros_healthd_utils_test_pkg_deps" ]
    deps = [
      ":libcros_healthd_utils",
      ":libcros_healthd_utils_test_utils",
      "//diagnostics/base",
      "//diagnostics/base:healthd_testrunner",
    ]
  }
}
