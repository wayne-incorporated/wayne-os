# Copyright 2021 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

group("all") {
  deps = [
    ":install_dbus_activation",
    ":install_dbus_config",
    ":install_seccomp_policy",
    ":install_service_manager_policy_config",
    ":install_upstart_config",
    ":rmad",
  ]
  if (use.test) {
    deps += [ ":rmad_test" ]
  }
}

pkg_config("target_defaults") {
  pkg_deps = [
    "libbrillo",
    "libchrome",
    "libmojo",
  ]
}

executable("rmad") {
  sources = [ "main.cc" ]
  configs += [ ":target_defaults" ]
  install_path = "sbin"
  deps = [
    "daemon:libdaemon",
    "executor:libexecutor",
    "interface:libinterface",
    "minijail:libminijail",
    "ssfc:libssfc",
    "utils:libutils",
  ]
}

# Install upstart configs and scripts.
install_config("install_upstart_config") {
  sources = [ "init/rmad.conf" ]
  install_path = "/etc/init"
}

# Install D-Bus configuration file.
install_config("install_dbus_config") {
  sources = [ "dbus/org.chromium.Rmad.conf" ]
  install_path = "/etc/dbus-1/system.d"
}

# Install D-Bus service activation configuration.
install_config("install_dbus_activation") {
  sources = [ "dbus/org.chromium.Rmad.service" ]
  install_path = "/usr/share/dbus-1/system-services"
}

# Install seccomp policy file.
install_config("install_seccomp_policy") {
  _arch = getenv("ARCH")

  # NOSORT
  sources = [
    "seccomp/rmad-seccomp-${_arch}.policy",
    "seccomp/rmad-executor-seccomp-${_arch}.policy",
  ]
  install_path = "/usr/share/policy"

  # NOSORT
  outputs = [
    "rmad-seccomp.policy",
    "rmad-executor-seccomp.policy",
  ]
}

# Install the policy for mojo service manager.
install_config("install_service_manager_policy_config") {
  sources = [ "init/mojo/cros_rmad.jsonc" ]
  install_path = "/etc/mojo/service_manager/policy"
}

if (use.test) {
  executable("rmad_test") {
    run_test = true
    deps = [
      "daemon:libdaemon_test",
      "executor:libexecutor_test",
      "interface:libinterface_test",
      "logs:liblogs_test",
      "metrics:libmetrics_test",
      "ssfc:libssfc_test",
      "state_handler:libstate_handler_test",
      "system:libsystem_test",
      "udev:libudev_test",
      "utils:libutils_test",
      "//common-mk/testrunner",
    ]
  }
}
