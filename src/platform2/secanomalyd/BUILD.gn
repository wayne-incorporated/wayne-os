# Copyright 2021 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

group("all") {
  deps = [
    ":install_minijail_conf",
    ":install_upstart_config",
    ":secanomalyd",
  ]
  if (use.test) {
    deps += [ ":secanomalyd_testrunner" ]
  }
}

pkg_config("target_defaults") {
  pkg_deps = [
    "libbrillo",
    "libchrome",
    "libmetrics",
    "libsession_manager-client",
    "vboot_host",
  ]
}

pkg_config("libsecanomaly_config") {
  pkg_deps = [ "re2" ]
}

source_set("landlock") {
  sources = [ "landlock.c" ]
}

static_library("libsecanomaly") {
  sources = [
    "audit_log_reader.cc",
    "daemon.cc",
    "metrics.cc",
    "mount_entry.cc",
    "mounts.cc",
    "processes.cc",
    "reporter.cc",
    "system_context.cc",
    "text_file_reader.cc",
  ]
  configs += [
    ":target_defaults",
    ":libsecanomaly_config",
  ]
  deps = [ ":landlock" ]
}

executable("secanomalyd") {
  sources = [ "main.cc" ]
  configs += [ ":target_defaults" ]
  install_path = "sbin"
  deps = [ ":libsecanomaly" ]
}

install_config("install_minijail_conf") {
  sources = [ "minijail/secanomalyd.conf" ]
  install_path = "/usr/share/minijail"
}

install_config("install_upstart_config") {
  sources = [ "secanomalyd.conf" ]
  install_path = "/etc/init"
}

if (use.test) {
  pkg_config("secanomalyd_testrunner_config") {
    pkg_deps = [ "libchrome-test" ]
  }

  executable("secanomalyd_testrunner") {
    sources = [
      "audit_log_reader_test.cc",
      "mount_entry_test.cc",
      "mounts_test.cc",
      "processes_test.cc",
      "reporter_test.cc",
      "text_file_reader_test.cc",
    ]
    configs += [
      "//common-mk:test",
      ":secanomalyd_testrunner_config",
      ":target_defaults",
    ]
    run_test = true
    deps = [
      ":libsecanomaly",
      "//common-mk/testrunner",
    ]
  }
}
