# Copyright 2018 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

group("all") {
  deps = [
    ":mount-passthrough",
    ":mount-passthrough-jailed",
  ]
  if (use.test) {
    deps += [ ":mount-passthrough-util_test" ]
  }
}

pkg_config("mount-passthrough_config") {
  pkg_deps = [
    "fuse",
    "libbrillo",
    "libcap",
    "libchrome",
  ]
}

pkg_config("mount-passthrough_jailed_config") {
  pkg_deps = [
    "libbrillo",
    "libchrome",
  ]
}

executable("mount-passthrough") {
  sources = [ "mount-passthrough.cc" ]
  defines = [ "USE_ARC_CONTAINER_P=${use.android_container_pi}" ]
  configs += [ ":mount-passthrough_config" ]
  install_path = "bin"
}

executable("mount-passthrough-jailed") {
  sources = [
    "mount-passthrough-jailed.cc",
    "mount-passthrough-util.cc",
  ]
  configs += [ ":mount-passthrough_jailed_config" ]
  install_path = "bin"
}

if (use.test) {
  executable("mount-passthrough-util_test") {
    sources = [
      "mount-passthrough-util.cc",
      "mount-passthrough-util_test.cc",
    ]
    configs += [
      "//common-mk:test",
      ":mount-passthrough_jailed_config",
    ]
    deps = [ "//common-mk/testrunner:testrunner" ]
    run_test = true
  }
}
