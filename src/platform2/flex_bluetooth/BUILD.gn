# Copyright 2023 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

group("all") {
  deps = [
    ":install_upstart_config",
    ":libflex_bluetooth_overrides",
    ":process_flex_bluetooth_overrides",
  ]
  if (use.test) {
    deps += [ ":flex_bluetooth_overrides_test" ]
  }
}

pkg_config("target_defaults") {
  pkg_deps = [
    "libbrillo",
    "libchrome",
  ]
}

static_library("libflex_bluetooth_overrides") {
  sources = [ "flex_bluetooth_overrides.cc" ]
  configs += [ ":target_defaults" ]
}

executable("process_flex_bluetooth_overrides") {
  sources = [ "process_flex_bluetooth_overrides.cc" ]
  install_path = "bin"
  configs += [ ":target_defaults" ]
  deps = [ ":libflex_bluetooth_overrides" ]
}

install_config("install_upstart_config") {
  sources = [ "init/flex-bluetooth.conf" ]
  install_path = "/etc/init"
}

if (use.test) {
  pkg_config("flex_bluetooth_overrides_test_config") {
    pkg_deps = [ "libchrome-test" ]
  }

  executable("flex_bluetooth_overrides_test") {
    sources = [ "flex_bluetooth_overrides_test.cc" ]
    configs += [
      "//common-mk:test",
      ":flex_bluetooth_overrides_test_config",
      ":target_defaults",
    ]
    deps = [
      ":libflex_bluetooth_overrides",
      "//common-mk/testrunner",
    ]
  }
}
